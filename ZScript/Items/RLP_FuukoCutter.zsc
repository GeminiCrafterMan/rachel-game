class RLP_FuukoCutter : CustomInventory
{
	Default
	{
		//$Category Items
		//$Title "Fuuko's Cutter
		Tag "$TAG_FUUKO_CUTTER";
		Inventory.PickupMessage "$PICKUP_FUUKO_CUTTER";
		Inventory.MaxAmount 1;
		Inventory.Amount 1;
		+INVENTORY.INVBAR;
	}
	States
	{
		Spawn:
			FCUT A -1;
			Stop;
		Use:
			TNT1 A 0 A_FireProjectile("RLP_ThrownCutter");
			Stop;
	}
}

class RLP_ThrownCutter : Actor
{
	Default
	{
		Speed 22;
		+RIPPER
		+NOGRAVITY
		+FLOAT
		+MISSILE
		Radius 16;
		Height 16;
		DamageFunction (15);
		DamageType "Cutter";
		Obituary "$OB_FUUKO_CUTTER";
	}
	States
	{
		Spawn:
			M000 AAAAAAAAAAAAAAAA 1 {self.SpriteRotation += 15;}
			M000 AAAAAAAA 1 {
				Vel.X /= 8;
				Vel.Z /= 4;
				self.SpriteRotation += 15;
			}
		ReturnToOwner:
			M000 A 0 {
				angle -= 180;
				tracer = target;
				bSEEKERMISSILE = true;
			}
		Returning:
			M000 A 0 {
				A_SeekerMissile(10, 10, SMF_PRECISE);
				self.SpriteRotation -= 15;
			}
			M000 A 1 A_JumpIfCloser(40, "Give");
			Loop;
		Death:
			Goto ReturnToOwner;
//			TNT1 A 0 {bFLOAT = false;}
//			M000 A 1 A_JumpIfCloser(40, "Give");
//			Loop;
		Give:
			M000 A 1 {
				if (target && target.player)
				{
//					A_SpawnItemEx("RLP_FuukoCutter");
					target.A_GiveInventory("RLP_FuukoCutter");
				}
			}
			Stop;
	}
}