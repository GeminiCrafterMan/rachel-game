class RLP_ScrapCannon : RLP_Weapon replaces SuperShotgun // for now, mostly so I can play through Doom games with RLP stuff lol
{
	Default
	{
		//$Category "Weapons"
		//$Title "Slayer's Kahuna
		Weapon.SelectionOrder 1900;
		+WEAPON.NOAUTOFIRE
		+DECOUPLEDANIMATIONS
		Inventory.PickupSound "weapons/kahuna/pickup";
		Inventory.PickupMessage "$PICKUP_SCRAPCANNON";

		Weapon.AmmoType1 "RLP_Scrap";
		Weapon.AmmoUse1 4;
		Weapon.AmmoGive1 16;
		Weapon.AmmoType2 "RLP_Scrap";
		Weapon.AmmoUse2 1;

		Tag "$TAG_SCRAPCANNON";
	}
	States
	{
		Ready:
			M000 A 1 {
				A_WeaponReady();
				invoker.SetAnimation('Idle', flags: SAF_INSTANT|SAF_NOOVERRIDE);
			}
			Loop;
		Select:
			M000 A 1 {
				A_OverlayFlags(OverlayID(), PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_PLAYERTRANSLATED, true);
				A_ChangeModel("RLP_ScrapCannon", 1, "Models/Player/Weapons", "Kahuna-hands.iqm", 1, "Patches/Models/Player", "rachel_base.png", 0, 0);
				A_Raise(18);
				invoker.SetAnimation('Idle', flags: SAF_INSTANT);
			}
			Loop;
		Deselect:
			M000 A 1 {
				A_Lower(18);
				invoker.SetAnimation('Idle', flags: SAF_INSTANT);
			}
			Loop;
		Fire:
			M000 A 6 Bright {
				A_ChangeVelocity(-Cos(pitch)*2, 0, sin(pitch)*2,CVF_RELATIVE);
				A_QuakeEx(1.5, 1.5, 1.5, 4, 0, 1, "", QF_SHAKEONLY);
				A_StartSound("weapons/scrap/fire", CHAN_AUTO);
				A_FireProjectile("RLP_FiredScrapCube", spawnofs_xy: 12, spawnheight: 4);
				invoker.SetAnimation('Fire');
			}
			M000 A 1;
			M000 A 10 {invoker.SetAnimation('Spin');}
			M000 A 10 {invoker.SetAnimation('Pump');}
			M000 A 9 A_StartSound("weapons/kahuna/pump1", CHAN_AUTO);
			M000 A 8 A_StartSound("weapons/kahuna/pump2", CHAN_AUTO);
			M000 A 0 A_ReFire;
			Goto Ready;
		AltFire:
			M000 A 6 Bright {
				A_ChangeVelocity(-Cos(pitch), 0, sin(pitch),CVF_RELATIVE);
				A_QuakeEx(0.75, 0.75, 0.75, 4, 0, 1, "", QF_SHAKEONLY);
				A_StartSound("weapons/scrap/fire", CHAN_AUTO);
				A_FireProjectile("RLP_FiredScrapChunkRandom", frandom(-1, 1), spawnofs_xy: 12, spawnheight: 4, pitch: frandom(-1, 1));
				invoker.SetAnimation('Fire');
			}
			M000 A 1;
			M000 A 5 {invoker.SetAnimation('Spin');}
			TNT1 A 0 A_ReFire;
			M000 A 10 {invoker.SetAnimation('Pump');}
			M000 A 9 A_StartSound("weapons/kahuna/pump1", CHAN_AUTO);
			M000 A 8 A_StartSound("weapons/kahuna/pump2", CHAN_AUTO);
			M000 A 0 A_ReFire;
			Goto Ready;
		Reload:
			Goto Ready;
		Spawn:
			SKSG A -1 NoDelay { // PW's SKSGZ0 sprite, used since UDB crashes when you go into 3D mode with our weapon models...?
				A_ChangeModel("RLP_ScrapCannon", 1, "", "", 1, "", "", 0, 0); // remove model
				invoker.SetAnimation('Spawn', flags: SAF_INSTANT);
			}
			Stop;
	}
}
class RLP_FiredScrapCube : Actor
{
	Default
	{
		Projectile;
		Radius 10;
		Height 10;
		DamageFunction (47);
		Speed 35;
		DamageType "Scrap";
		Obituary "$OB_SCRAPCUBE";
	}
	States
	{
		Spawn:
			DUST A 4;
			Loop;
		Crash:
		XDeath:
		Death:
			TNT1 A 0 A_StartSound("weapons/scrap/split", CHAN_BODY);
//			TNT1 A 6 A_SpawnItemEx("ExplosionEffect1",0,0,0,0,0,0,0,SXF_WEPFXCOLOR);
			TNT1 A 0 A_SpawnItemEx("RLP_FiredScrapChunk1",0,0,0,28,0,0,45);
			TNT1 A 0 A_SpawnItemEx("RLP_FiredScrapChunk2",0,0,0,28,0,0,-45);
			TNT1 A 0 A_SpawnItemEx("RLP_FiredScrapChunk3",0,0,0,28,0,0,225);
			TNT1 A 0 A_SpawnItemEx("RLP_FiredScrapChunk4",0,0,0,28,0,0,-225);
			Stop;
	}
}
class RLP_FiredScrapChunkRandom : RLP_FiredScrapChunk1
{
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(256, "Chunk1", "Chunk2", "Chunk3", "Chunk4");
			Stop;
		Chunk1:
			DUST B 3;
			DUST B 0 {bTHRUACTORS = false;}
			DUST B 4;
			Loop;
		Chunk2:
			DUST C 3;
			DUST C 0 {bTHRUACTORS = false;}
			DUST C 4;
			Loop;
		Chunk3:
			DUST D 3;
			DUST D 0 {bTHRUACTORS = false;}
			DUST D 4;
			Loop;
		Chunk4:
			DUST E 3;
			DUST E 0 {bTHRUACTORS = false;}
			DUST E 4;
			Loop;
	}
}
class RLP_FiredScrapChunk1 : Actor
{
	Default
	{
		Projectile;
		+THRUACTORS
		Radius 6;
		Height 6;
		DamageFunction (20);
		DamageType "Scrap";
		Obituary "$OB_SCRAPCHUNK";
		Speed 28;
	}
	States
	{
		Spawn:
			DUST B 3;
			DUST B 0 {bTHRUACTORS = false;}
			DUST B 4;
			Loop;
	}
}
class RLP_FiredScrapChunk2 : RLP_FiredScrapChunk1
{
	States
	{
		Spawn:
			DUST C 3;
			DUST C 0 {bTHRUACTORS = false;}
			DUST C 4;
			Loop;
	}
}
class RLP_FiredScrapChunk3 : RLP_FiredScrapChunk1
{
	States
	{
		Spawn:
			DUST D 3;
			DUST D 0 {bTHRUACTORS = false;}
			DUST D 4;
			Loop;
	}
}
class RLP_FiredScrapChunk4 : RLP_FiredScrapChunk1
{
	States
	{
		Spawn:
			DUST E 3;
			DUST E 0 {bTHRUACTORS = false;}
			DUST E 4;
			Loop;
	}
}