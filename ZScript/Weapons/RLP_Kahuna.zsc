class RLP_KahunaSpread : Inventory {Default{Inventory.MaxAmount 24;}}
class RLP_Kahuna : RLP_Weapon replaces Shotgun // for now, mostly so I can play through Doom games with RLP stuff lol
{
	Default
	{
		//$Category "Weapons"
		//$Title "Slayer's Kahuna
		Weapon.SelectionOrder 1900;
		+WEAPON.NOAUTOFIRE
		+DECOUPLEDANIMATIONS
		Inventory.PickupSound "weapons/kahuna/pickup";
		Inventory.PickupMessage "$PICKUP_KAHUNA";
		Tag "$TAG_KAHUNA";
		Obituary "$OB_KAHUNA";
		Weapon.AmmoType1 "RLP_Shell";
		Weapon.AmmoUse1 1;
		Weapon.AmmoGive1 9;
		Weapon.AmmoType2 "RLP_Shell";
		Weapon.AmmoUse2 1;
	}
	States
	{
		Ready:
			M000 A 1 {
				A_WeaponReady();
				invoker.SetAnimation('Idle', flags: SAF_INSTANT|SAF_NOOVERRIDE);
			}
			Loop;
		Select:
			M000 A 1 {
				A_OverlayFlags(OverlayID(), PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_PLAYERTRANSLATED, true);
				A_ChangeModel("RLP_Kahuna", 1, "Models/Player/Weapons", "Kahuna-hands.iqm", 1, "Patches/Models/Player", "rachel_base.png", 0, 0);
				A_Raise(18);
				invoker.SetAnimation('Idle', flags: SAF_INSTANT);
			}
			Loop;
		Deselect:
			M000 A 1 {
				A_Lower(18);
				invoker.SetAnimation('Idle', flags: SAF_INSTANT);
			}
			Loop;
		Fire:
			M000 A 6 Bright {
				A_ChangeVelocity(-Cos(pitch)*2, 0, sin(pitch)*2,CVF_RELATIVE);
				A_QuakeEx(1.5, 1.5, 1.5, 4, 0, 1, "", QF_SHAKEONLY);
				A_StartSound("weapons/kahuna/fire", CHAN_AUTO);
				A_FireBullets((6+(Countinv("RLP_KahunaSpread")*0.4)), (6+(Countinv("RLP_KahunaSpread")*0.4)), 12, 10, "BulletPuff", FBF_USEAMMO|FBF_NORANDOM, missile: "RLP_Tracer", spawnheight: 4, spawnofs_xy: 7);
				invoker.SetAnimation('Fire');
			}
			M000 A 1;
			M000 A 10 {invoker.SetAnimation('Pump');}
			M000 A 9 A_StartSound("weapons/kahuna/pump1", CHAN_AUTO);
			M000 A 8 A_StartSound("weapons/kahuna/pump2", CHAN_AUTO);
			M000 A 0 A_ReFire;
			Goto Ready;
		AltFire:
			M000 A 1 Bright {
				A_StartSound("weapons/kahuna/fire", CHAN_AUTO);
				A_SetPitch(pitch+0.80);
				invoker.SetAnimation('Fire');
				A_FireBullets((5+(Countinv("RLP_KahunaSpread")*0.8)), (5+(Countinv("RLP_KahunaSpread")*0.8)), 12, 11, "BulletPuff", FBF_USEAMMO|FBF_NORANDOM, missile: "RLP_Tracer", spawnheight: 4, spawnofs_xy: 7);
				A_ChangeVelocity(-Cos(pitch)*2.5, 0, sin(pitch)*2.5,CVF_RELATIVE);
//				A_SpawnItemEx("GWM_Flash");
//				A_SpawnItemEx("GWM_MuzzleFlashLight2",0,0,5);
				A_QuakeEx(1.5, 1.5, 1.5, 4, 0, 1, "", QF_SHAKEONLY);
				A_GiveInventory("RLP_KahunaSpread",2); // important
			}
			M000 A 1 Bright A_SetPitch(pitch+0.80);
			M000 AAAAA 1 Bright A_SetPitch(pitch-0.40);
			M000 A 1 A_SetPitch(pitch-0.40);
			M000 A 5 {invoker.SetAnimation('Spin');}
			TNT1 A 0 A_ReFire;
			M000 A 10 {invoker.SetAnimation('Pump');}
			M000 A 9 A_StartSound("weapons/kahuna/pump1", CHAN_AUTO);
			M000 A 8 A_StartSound("weapons/kahuna/pump2", CHAN_AUTO);
			M000 A 0 A_ReFire;
			Goto Ready;
		Spawn:
			SKSG A -1 NoDelay { // PW's SKSGZ0 sprite, used since UDB crashes when you go into 3D mode with our weapon models...?
				A_ChangeModel("RLP_Kahuna", 1, "", "", 1, "", "", 0, 0); // remove model
				invoker.SetAnimation('Spawn', flags: SAF_INSTANT);
			}
			Stop;
	}
}
class RLP_Tracer : FastProjectile
{
	Default
	{
		RenderStyle "Add";
		Radius 2;
		Height 2;
		Speed 180;
		//+BLOODSPLATTER
		+HITTRACER
		+NOINTERACTION
		Scale .5;
		MissileHeight 8;
	}
	States
	{
		Dummy:
			M000 A 1 Bright;
			Loop;
		Spawn:
			M000 A 1 BRIGHT;
			Loop;
		Crash:
			TNT1 A 0 A_Jump(256,"Death");
		Death:
			PUFF A 2 BRIGHT/* A_SpawnDebris("RLP_TracerSpark")*/;
			PUFF B 1 BRIGHT A_SetTranslucent(0.9,0);
			PUFF B 1 BRIGHT A_StartSound("bullet/ricochet",CHAN_BODY,.5);
			PUFF CD 2 BRIGHT;
			Stop;
		XDeath:
			TNT1 A 3 A_StartSound("bullet/hitFlesh",CHAN_BODY,.5);
			Stop;
	}
}