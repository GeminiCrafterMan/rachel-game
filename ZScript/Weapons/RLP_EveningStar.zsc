class RLP_EveningStar : RLP_Weapon replaces Pistol
{
	Default
	{
		Weapon.SelectionOrder 1900;
		+WEAPON.WIMPY_WEAPON
		+WEAPON.NOAUTOFIRE
		Inventory.PickupMessage "$PICKUP_EVENING_STAR";
		Tag "$TAG_EVENING_STAR";
		+DECOUPLEDANIMATIONS
	}
	States
	{
		Ready:
			M000 A 1 {
				A_WeaponReady();
				invoker.SetAnimation('Idle', flags: SAF_NOOVERRIDE);
			}
			Loop;
		Select:
			M000 A 1 {
				A_OverlayFlags(OverlayID(), PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_PLAYERTRANSLATED, true);
				A_ChangeModel("RLP_EveningStar", 1, "Models/Player/Weapons", "Evening Star-hands.iqm", 1, "Patches/Models/Player", "rachel_base.png", 0, 0);
				A_Raise(18);
				invoker.SetAnimation('Idle', flags: SAF_INSTANT);
			}
			Loop;
		Deselect:
			M000 A 1 {
				A_Lower(18);
				invoker.SetAnimation('Idle', flags: SAF_INSTANT);
			}
			Loop;
		Fire:
			M000 A 4 {
				A_StartSound("weapons/evening_star/fire", CHAN_AUTO);
				A_FireProjectile("RLP_EveningStar_Projectile", spawnofs_xy: 7, spawnheight: 2);
				invoker.SetAnimation('Fire');
			}
			M000 AAAAAA 1 A_JumpIf(JustPressed(BT_ATTACK), "Fire");
			M000 A 0 A_ReFire;
			Goto Ready;
		Spawn:
			M000 A -1 NoDelay {
				A_ChangeModel("RLP_EveningStar", 1, "", "", 1, "", "", 0, 0); // clear model
				invoker.SetAnimation('Spawn', flags: SAF_INSTANT);
			}
			Stop;
	}
}
class RLP_EveningStar_Projectile : FastProjectile
{
	Default
	{
		RenderStyle "Add";
		Scale 0.5;
		Radius 11;
		Height 8;
		Speed 100;
		DamageFunction (20);
		DeathSound "weapons/evening_star/explode";
		Obituary "$OB_EVENING_STAR";
//		+FORCEXYBILLBOARD
//		+BILLBOARDFACECAMERA // fuck your settings, it looks awkward
// gem from the not-so-distant future here; it looks even worse when billboarded...
	}
	States
	{
		Spawn:
			M000 AB 1 Bright;
			Loop;
		Death:
			TNT1 A 0 {Self.Scale.Y = 0.6;}
			PLSE A 5 Bright;
			PLSE B 4 Bright;
			PLSE C 3 Bright;
			PLSE D 2 Bright;
			PLSE E 1 Bright;
			Stop;
	}
}