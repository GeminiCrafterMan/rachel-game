class RLP_BlasterA : RLP_Weapon
{
	Default
	{
		Weapon.SelectionOrder 1900;
		+WEAPON.WIMPY_WEAPON
		+WEAPON.NOAUTOFIRE
		Inventory.PickupMessage "$PICKUP_BLASTER_A";
		Tag "$TAG_BLASTER_A";
		+DECOUPLEDANIMATIONS
	}
	States
	{
		Ready:
			M000 A 1 {
				A_WeaponReady();
				invoker.SetAnimation('Idle', flags: SAF_LOOP|SAF_NOOVERRIDE);
			}
			Loop;
		Select:
			M000 A 1 {
				A_Raise(18);
				A_OverlayFlags(OverlayID(), PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_PLAYERTRANSLATED, true);
				invoker.SetAnimation('Idle', flags: SAF_LOOP|SAF_NOOVERRIDE);
			}
			Loop;
		Deselect:
			M000 A 1 {
				A_Lower(18);
				invoker.SetAnimation('Idle', flags: SAF_LOOP|SAF_NOOVERRIDE);
			}
			Loop;
		Fire:
			M000 A 4 {
				A_StartSound("weapons/blaster_a/fire", CHAN_AUTO);
				A_FireProjectile("RLP_BlasterA_Projectile", spawnofs_xy: 8);
				invoker.SetAnimation('Fire');
//				A_GunFlash();
			}
			M000 AAAAAA 1 A_JumpIf(JustPressed(BT_ATTACK), "Fire");
			M000 A 0 A_ReFire;
			Goto Ready;
//		Flash:
//			PLSE ABCDE 2 Bright Offset(64, 64);
//			Stop;
		Spawn:
			M000 A -1 {invoker.SetAnimation('Spawn', flags: SAF_LOOP|SAF_NOOVERRIDE);}
			Stop;
	}
}
class RLP_BlasterA_Projectile : FastProjectile
{
	Default
	{
		RenderStyle "Add";
		Scale 0.5;
		Radius 11;
		Height 8;
		Speed 100;
		DamageFunction (20);
		DeathSound "weapons/blaster_a/explode";
		Obituary "$OB_BLASTER_A";
	}
	States
	{
		Spawn:
			M000 AB 1 Bright;
			Loop;
		Death:
			PLSE A 5 Bright;
			PLSE B 4 Bright;
			PLSE C 3 Bright;
			PLSE D 2 Bright;
			PLSE E 1 Bright;
			Stop;
	}
}